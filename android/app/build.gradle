def keystorePropertiesFile = rootProject.file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
apply plugin: 'com.android.application'

android {
    namespace "com.wetterheidi.dzmaster"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.wetterheidi.dzmaster"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 29
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        release {
            storeFile rootProject.file(keystoreProperties.storeFile)
            storePassword keystoreProperties.storePassword
            keyAlias keystoreProperties.keyAlias
            keyPassword keystoreProperties.keyPassword
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            packagingOptions {
                jniLibs {
                    useLegacyPackaging true
                }
                resources {
                    excludes.add("META-INF/androidx.baselineprofile.xml")
                }
            }
        }
        release {
         // Enables code-related app optimization.
            minifyEnabled true

            // Enables resource shrinking.
            shrinkResources true

            // Default file with automatically generated optimization rules.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

            // Wichtig: Debug-Ausgaben im Release-Build aktivieren
            debuggable false

            // Zusätzliche Optimierungen deaktivieren
            proguardFiles 'proguard-rules.pro'

            ndk {
                debugSymbolLevel 'FULL'
            }
            signingConfig signingConfigs.release
        }
    }
    
    // Verhindern von Race Conditions beim Start
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation project(':capacitor-android')
    implementation project(':capacitor-browser') // <-- HINZUGEFÜGT
    implementation "androidx.core:core-splashscreen:1.0.0"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
